plugins {
    id 'java'
    id 'io.github.lognet.grpc-spring-boot' version '4.9.0'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version "1.1.0"
    id 'com.google.protobuf' version '0.8.19'
}

version = "0.1"
group = "helloworld.grpc"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.lognet:grpc-spring-boot-starter:4.9.0'
}

java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

sourceSets {
    main {
        java {
            srcDirs("build/protoGen/main/grpc")
            srcDirs("build/protoGen/main/java")
        }
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.21.7" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.50.0" }
    }

    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

configurations {
    newrelic
}

dependencies {
    newrelic 'com.newrelic.agent.java:newrelic-agent:7.11.1'
}

bootRun {
    afterEvaluate {
        jvmArgs = [
                "-javaagent:${configurations.newrelic.singleFile}"
        ]
        environment = [
                "NEW_RELIC_APP_NAME"                             : "local_" + project.name + "_sb",
                "NEW_RELIC_LICENSE_KEY"                          : project.properties["newrelic_license_key"],
                "NEW_RELIC_DISTRIBUTED_TRACING_ENABLED"          : "true",
                "NEW_RELIC_ERROR_COLLECTOR_EXPECTED_STATUS_CODES": "3,9,400-499"
        ]
    }
}
